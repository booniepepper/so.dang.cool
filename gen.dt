#!/usr/bin/env dt

[ "}}" split deq [code literal]:
  code eval
  literal push "" join
] \fill-part def!

[ "{{" split deq 
  \fill-part map enq
  "" join
] \fill def!


### Blog ###

"blog" cd

"blog.html.template" readf \blog-template:

[ [title main]: blog-template fill ] \blog def!

# TODO: dirname/basename should be in dt
[ "/" split last ] \basename def

[ "." split pop drop "." join ] \drop-ext def

[ "so.dang.cool ::" push rev unwords ] \as-title def


### Blog posts ###

"posts" cd ls ".." cd
[ ".part" ends-with? ] filter
[ \filename:
  "posts" filename push "/" join \source:
  filename drop-ext \html-filename:
  html-filename drop-ext
  "-" split \deq 3 times unwords \post-title:
  \enq 2 times "-" join \date:

  ### Render the post ###
  [ post-title date ] \do map " :: " join
  source readf fill
  blog html-filename writef

  ### Return the list item for index ###
  [ "  <li><a href='" html-filename "'>" date " :: " post-title "</a></li>" ] \do map
  "" join
] map
sort rev unlines \posts:


### Blog index ###

"Boonie Pepper Bites" as-title
[ "<ul>" posts "</ul>" ] [do "      " push rev "" join] map unlines
blog "index.html" writef
