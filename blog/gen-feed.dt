#!/usr/bin/env dt

[ "}}" split deq [code literal]:
  code eval
  literal push "" join
] \fill-part def!

[ "{{" split deq
  \fill-part map enq
  "" join
] \fill def!

"https://so.dang.cool/blog/feed.xml" \feed-url:
"https://so.dang.cool/blog" \blog-url:
"date -Iminutes" exec lines first \updated-at:

### entries ###

"posts" cd ls ".." cd
[".html.part" ends-with?] filter
[".html.part" split first] map
sort rev
[ \filename:
  blog-url filename push "/" join \post-url:
  filename "." split first "-" split \deq 3 times unwords \title:
  enq enq "-" join "T00:00:00-08:00" push "" join \published-at:

  "feed-entry.xml.template" readf fill
] map unlines
lines ["" eq? not] filter
unlines \entries:

"feed.xml.template" readf
fill

"feed.xml" writef
